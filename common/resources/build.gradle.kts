import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

plugins {
    // alias(libs.plugins.android.library)
    alias(libs.plugins.android.kotlinMultiplatformLibrary)
    alias(libs.plugins.google.ksp)
    alias(libs.plugins.jetbrains.compose)
    alias(libs.plugins.jetbrains.composeCompiler)
    alias(libs.plugins.jetbrains.kotlinMultiplatform)
    alias(libs.plugins.sergiobelda.gradle.common.library.android)
    alias(libs.plugins.sergiobelda.gradle.dependencyGraphGenerator)
    alias(libs.plugins.sergiobelda.gradle.lint)
}

kotlin {
    androidLibrary {
        androidResources.enable = true

        namespace = "dev.sergiobelda.todometer.common.resources"
    }
    jvm()
    iosX64()
    iosArm64()
    iosSimulatorArm64()

    sourceSets {
        commonMain.dependencies {
            implementation(libs.jetbrains.compose.ui)
            api(libs.lyricist.lyricist)
        }

        all {
            languageSettings.optIn("kotlin.RequiresOptIn")
        }
    }
}

// region Lyricist Multiplatform setup
dependencies {
    add("kspCommonMainMetadata", libs.lyricist.processor)
}

// Workaround for KSP only in Common Main.
// https://github.com/google/ksp/issues/567
tasks.withType<KotlinCompilationTask<*>>().all {
    if (name != "kspCommonMainKotlinMetadata") {
        dependsOn("kspCommonMainKotlinMetadata")
    }
}

kotlin.sourceSets.commonMain {
    kotlin.srcDir("build/generated/ksp/metadata/commonMain/kotlin")
}
// endregion
